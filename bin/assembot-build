#!/usr/bin/env coffee

cli= require 'commander'
pkg= require '../package'
{assembot, _, loadTargets}= require '../lib/index'
log= require '../lib/log'

cli
  .version(pkg.version)
  .option('-v, --verbose', 'force installation')
  .option('-q, --quiet', 'force installation')
  .option('-t, --target <filepath>', 'build specified target only')
  .parse(process.argv);

log.level 0 if cli.quiet
log.level 2 if cli.verbose

log.info ''
log.info "Building..."

all_targets= loadTargets()
targets= []

if cli.target?
  if options= all_targets[cli.target]
    log.info " only #{ cli.target }:"
    bot= assembot cli.target, options
    bot.build().then ->
      log.info "Done"
  else
    log.say "Target '#{ cli.target }' not found."

else
  targets.push {target, options} for target, options of all_targets

  buildIt= ->
    if targets.length is 0
      log.info "Done"
    else
      {target, options}= targets.shift()
      log.debug "Target:", target
      log.debug "Configuration:"
      log.debug options
      bot= assembot target, options
      bot.build().then buildIt

  buildIt()
