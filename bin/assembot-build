#!/usr/bin/env coffee

cli= require 'commander'
pkg= require '../package'
{assembot, _, loadTargets, loadOptions}= require '../lib/index'
log= require '../lib/log'
notify= require '../lib/notify'

cli
  .version(pkg.version)
  .option('-v, --verbose', 'force installation')
  .option('-q, --quiet', 'force installation')
  .option('-t, --target <filepath>', 'build specified target only')
  .parse(process.argv);

log.level 0 if cli.quiet
log.level 2 if cli.verbose

log.info ''
log.info "Building..."

options= loadOptions()
all_targets= loadTargets()
targets= []

if cli.target?
  if opts= all_targets[cli.target]
    log.info " only #{ cli.target }:"
    bot= assembot cli.target, opts
    bot.build().then ->
      notify.emit 'done', options
      log.info "Done"
  else
    log.say "Target '#{ cli.target }' not found."

else
  targets.push {target, opts} for target, opts of all_targets

  buildIt= ->
    if targets.length is 0
      notify.emit 'done', options # TODO: FIX ME!
      log.info "Done"
    else
      {target, opts}= targets.shift()
      log.debug "Target:", target
      log.debug "Configuration:"
      log.debug opts
      bot= assembot target, opts
      bot.build().then buildIt

  buildIt()
